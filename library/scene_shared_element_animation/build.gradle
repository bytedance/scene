apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.miniSdkVersion
    }
    resourcePrefix 'scene_'
}

dependencies {
    implementation APPCOMPAT_LIB
    implementation TRANSITION_SUPPORT_LIB
    implementation LIFECYCLE_RUNTIME_LIB
    api project(path: ':library:scene')
    api project(path: ':library:scene_navigation')
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("../javadoc/scene_shared_element_animation/")
    failOnError false
    title = null

    options {
        doclet = "com.google.doclava.Doclava"
        docletpath(new File(project.rootDir, "tools/doclava-1.0.6.jar"))
        locale "en_US"
        encoding "UTF-8"
    }
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.bytedance.scene'
                artifactId = 'scene_shared_element_animation'
                version = '1.3.0'
            }
        }
    }
}